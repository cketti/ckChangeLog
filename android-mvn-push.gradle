if (!(hasProperty("nexusUsername") && hasProperty("nexusPassword"))) {
    return;
}

apply plugin: 'maven'
apply plugin: 'signing'

afterEvaluate { project ->
    project.version = android.defaultConfig.versionName
    project.group = project.pom.group

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                pom.artifactId = project.pom.artifactId

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.project {
                    name project.pom.name
                    packaging 'aar'
                    description project.pom.description
                    url project.pom.url

                    scm {
                        url project.pom.scmUrl
                        connection project.pom.scmConnection
                        developerConnection project.pom.scmDevConnection
                    }

                    licenses {
                        license {
                            name project.pom.licenseName
                            url project.pom.licenseUrl
                            distribution project.pom.licenseDist
                        }
                    }

                    developers {
                        developer {
                            id project.pom.developerId
                            name project.pom.developerName
                        }
                    }
                }
            }
        }
    }

    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

        options {
            linksOffline "http://d.android.com/reference", "${android.sdkDirectory}/docs/reference"
        }
    }

    task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}
